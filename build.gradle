allprojects {
    group = 'nl.oscar.kwetter'
    version = '0.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'war'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        providedCompile group: 'javax', name: 'javaee-api', version: '8.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compileOnly 'org.projectlombok:lombok:1.16.20'

        //apt "org.projectlombok:lombok:1.16.20"

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    }

    test {
        reports.getJunitXml().setDestination(file("build/test-reports"))
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    task integrationTest(type: Test) {
        setTestClassesDirs(sourceSets.integrationTest.getOutput().getClassesDirs())
        setClasspath(sourceSets.integrationTest.getRuntimeClasspath())

        reports.getJunitXml().setDestination(file("build/integration-test-reports"))
    }

    //check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}